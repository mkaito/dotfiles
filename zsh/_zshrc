# {{{ Environment
export PATH="${HOME}/dev/bin:${HOME}/.cabal/bin:${HOME}/.gem/ruby/1.9.1/bin:"${PATH}
export HISTFILE="${HOME}/.zsh_history"
export HISTSIZE=10000
export SAVEHIST=10000
export LESSHISTFILE="-"
export PAGER="less"
export READNULLCMD="${PAGER}"
export VISUAL="vim"
export EDITOR="vim"
export ALTERNATE_EDITOR=””
export BROWSER="firefox"
export XTERM="urxvt"
export PERL_SIGNALS=unsafe
export WINEDEBUG=-all

export ANDROIDSDK="/opt/android-sdk"
export ANDROIDNDK="/opt/android-ndk"
export ANDROIDNDKVER=r8
export ANDROIDAPI=8

export LEDGER_FILE="~/ledger.ldg"
# }}}

source ~/dev/dotfiles/zsh/_alias

# {{{ ZSH settings
setopt emacs
setopt nohup
setopt autocd
setopt cdablevars
setopt nobgnice
setopt nobanghist
setopt noclobber
setopt shwordsplit
setopt interactivecomments
setopt autopushd pushdminus pushdsilent pushdtohome
setopt histreduceblanks histignorespace inc_append_history

# Prompt requirements
setopt extended_glob prompt_subst
autoload colors zsh/terminfo
autoload -U zmv

# New style completion system
fpath=(/home/chris/dev/dotfiles/zsh/completion $fpath)
autoload -U compinit; compinit
compdef '_dispatch git git' hub
#  * List of completers to use
zstyle ":completion:*" completer _complete _match _approximate
#  * Allow approximate
zstyle ":completion:*:match:*" original only
zstyle ":completion:*:approximate:*" max-errors 1 numeric
#  * Selection prompt as menu
zstyle ":completion:*" menu select=1
#  * Menu selection for PID completion
zstyle ":completion:*:*:kill:*" menu yes select
zstyle ":completion:*:kill:*" force-list always
zstyle ":completion:*:processes" command "ps -au$USER"
zstyle ":completion:*:*:kill:*:processes" list-colors "=(#b) #([0-9]#)*=0=01;32"
#  * Don't select parent dir on cd
zstyle ":completion:*:cd:*" ignore-parents parent pwd
#  * Complete with colors
zstyle ":completion:*" list-colors ""
# }}}

# {{{ Key fixes
# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
typeset -A key

key[Home]=${terminfo[khome]}
key[End]=${terminfo[kend]}
key[Insert]=${terminfo[kich1]}
key[Delete]=${terminfo[kdch1]}
key[Up]=${terminfo[kcuu1]}
key[Down]=${terminfo[kcud1]}
key[Left]=${terminfo[kcub1]}
key[Right]=${terminfo[kcuf1]}
key[PageUp]=${terminfo[kpp]}
key[PageDown]=${terminfo[knp]}

for k in ${(k)key} ; do
	# $terminfo[] entries are weird in ncurses application mode...
	[[ ${key[$k]} == $'\eO'* ]] && key[$k]=${key[$k]/O/[}
done
unset k

# setup key accordingly
[[ -n "${key[Home]}"    ]]  && bindkey  "${key[Home]}"    beginning-of-line
[[ -n "${key[End]}"     ]]  && bindkey  "${key[End]}"     end-of-line
[[ -n "${key[Insert]}"  ]]  && bindkey  "${key[Insert]}"  overwrite-mode
[[ -n "${key[Delete]}"  ]]  && bindkey  "${key[Delete]}"  delete-char
[[ -n "${key[Up]}"      ]]  && bindkey  "${key[Up]}"      up-line-or-history
[[ -n "${key[Down]}"    ]]  && bindkey  "${key[Down]}"    down-line-or-history
[[ -n "${key[Left]}"    ]]  && bindkey  "${key[Left]}"    backward-char
[[ -n "${key[Right]}"   ]]  && bindkey  "${key[Right]}"   forward-char
# }}}

# {{{ Functions
function web ()   { "${BROWSER}" "http://yubnub.org/parser/parse?command=${*}" }
function pmem ()  { ps -o rss,comm -p `pgrep "$1"` }
function dsync () { rsync -lprt --progress --stats --delete "$1/" "$2/" }

function snap () {
		[ "$2" ] && tmout="$2"  || tmout=5
		[ "$3" ] && format="$3" || format=png
		fname="${HOME}/$1-`date +%d%m%y-%H%M`"
		for ((i=${tmout}; i>=1; i--)) do; echo -n "${i}.. "; sleep 1; done
		import -window root -quality 100 "${fname}.${format}"
		convert -resize "15%" "${fname}.${format}" "${fname}.th.${format}"
		echo ": ${fname}.${format}"
}

function d () {
	 [ "$1" ] && sid="$1" 
	 [ "$2" ] && sact="$2" || sact=start
	 bspath="/etc/rc.d/"

	 if [[ -f "${bspath}${sid}" ]]; then
			sudo "${bspath}${sid}" "${sact}";
	 else
			echo "Service $1 not found"
	 fi

	 sid=""
	 sact=""
}

#. ${HOME}/dev/bin/z.sh
#function precmd () {
	#z --add "$(pwd -P)"
#}

# {{{ Terminal and prompt
function preexec () {
	 # Screen window titles as currently running programs
	 if [[ "${TERM}" == "screen-256color" ]]; then
			 local CMD="${1[(wr)^(*=*|sudo|-*)]}"
			 echo -n "\ek$CMD\e\\"
	 fi
}

function setprompt () {
    if [[ "${terminfo[colors]}" -ge 8 ]]; then
        colors
    fi
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
	eval PR_"${color}"="%{${terminfo[bold]}$fg[${(L)color}]%}"
	eval PR_LIGHT_"${color}"="%{$fg[${(L)color}]%}"
    done
    PR_NO_COLOUR="%{${terminfo[sgr0]}%}"

    # Try to use extended characters to look nicer
    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
    PR_SET_CHARSET="%{${terminfo[enacs]}%}"
    PR_SHIFT_IN="%{${terminfo[smacs]}%}"
    PR_SHIFT_OUT="%{${terminfo[rmacs]}%}"
    PR_HBAR="${altchar[q]:--}"
    PR_ULCORNER="${altchar[l]:--}"
    PR_LLCORNER="${altchar[m]:--}"
    PR_LRCORNER="${altchar[j]:--}"
    PR_URCORNER="${altchar[k]:--}"

    # Terminal prompt settings
    case "${TERM}" in
        dumb) # Simple prompt for dumb terminals
            unsetopt zle
            PROMPT='%n@%m:%~%% '
            ;;
        linux) # Simple prompt with Zenburn colors for the console
            echo -en "\e]P01e2320" # zenburn black (normal black)
            echo -en "\e]P8709080" # bright-black  (darkgrey)
            echo -en "\e]P1705050" # red           (darkred)
            echo -en "\e]P9dca3a3" # bright-red    (red)
            echo -en "\e]P260b48a" # green         (darkgreen)
            echo -en "\e]PAc3bf9f" # bright-green  (green)
            echo -en "\e]P3dfaf8f" # yellow        (brown)
            echo -en "\e]PBf0dfaf" # bright-yellow (yellow)
            echo -en "\e]P4506070" # blue          (darkblue)
            echo -en "\e]PC94bff3" # bright-blue   (blue)
            echo -en "\e]P5dc8cc3" # purple        (darkmagenta)
            echo -en "\e]PDec93d3" # bright-purple (magenta)
            echo -en "\e]P68cd0d3" # cyan          (darkcyan)
            echo -en "\e]PE93e0e3" # bright-cyan   (cyan)
            echo -en "\e]P7dcdccc" # white         (lightgrey)
            echo -en "\e]PFffffff" # bright-white  (white)
            PROMPT='$PR_GREEN%n@%m$PR_WHITE:$PR_YELLOW%l$PR_WHITE:$PR_RED%~$PR_YELLOW%%$PR_NO_COLOUR '
            ;;
        *)  # Main prompt
            PROMPT="$PR_SET_CHARSET$PR_GREEN%B%n%b@$PR_NO_COLOUR%m %~
%(!.$PR_RED#$PR_NO_COLOUR.$PR_YELLOW→$PR_NO_COLOUR)$PR_NO_COLOUR "
            RPROMPT=""
    esac
}



# Prompt init
setprompt
# }}}
# }}}

#{{{ SSH Agent
ssh-add -l > /dev/null || ssh-add
#}}}

export PERL_LOCAL_LIB_ROOT="/home/chris/perl5";
export PERL_MB_OPT="--install_base /home/chris/perl5";
export PERL_MM_OPT="INSTALL_BASE=/home/chris/perl5";
export PERL5LIB="/home/chris/perl5/lib/perl5/x86_64-linux-thread-multi:/home/chris/perl5/lib/perl5";
export PATH="/home/chris/perl5/bin:$PATH";

# vim:ft=sh:fdm=marker
