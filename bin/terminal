#!/usr/bin/env zsh
set -euo pipefail

##
# Set $terminal to one of:
#   * urxvt
#   * st
#   * termite
#   * alacritty
#   * foot
#   * kitty
if [[ ${XDG_SESSION_TYPE:-} == wayland ]]; then
  terminal=foot
else
  terminal=alacritty
fi

##
# Set this to prefix all execution arguments.
# MUST be an array
# For example, set it to `zsh -ic` to run commands inside a zsh shell
precmd=(zsh -ic)

##
# `terminal chat weechat`
#   => `urxvtc -name chat -e weechat`
#   => `termite --name=chat -- weechat`
#   => `st -n chat -e weechat`
#   => `alacritty -t chat -e weechat`
#   => `foot -a chat -- weechat`
declare -a args
case $terminal in
  st)
    if [[ -n ${1:-} ]]; then
      args+=(-n "$1"); shift
      args+=(-e "${precmd[@]}" "$*")
    fi

    exec st "${args[@]}"
    ;;

  urxvt)
    if [[ -n ${1:-} ]]; then
      args+=(-name "$1"); shift
      args+=(-e "${precmd[@]}" "$*")
    fi

    pgrep -c urxvtd || urxvtd -q -o -f
    exec urxvtc "${args[@]}"

    ;;

  termite)
    if [[ -n ${1:-} ]]; then
      args+=("--name=$1"); shift
      args+=("--" "${precmd[@]}" "$*")
    fi

    exec termite "${args[@]}"
    ;;

  alacritty)
    if [[ -n ${1:-} ]]; then
      args+=(--class "$1"); shift
      args+=(-e "${precmd[@]}" "$*")
    fi

    exec alacritty "${args[@]}"
    ;;

  foot)
    if [[ -n ${1:-} ]]; then
      args+=("--app-id=$1"); shift
      args+=("--" "${precmd[@]}" "$*")
    fi

    exec foot "${args[@]}"
    ;;

  kitty)
    if [[ -n ${1:-} ]]; then
      args+=("--class=$1"); shift
      args+=("--" "${precmd[@]}" "$*")
    fi

    exec kitty "${args[@]}"
    ;;

  *)
    exit 1
    ;;
esac
